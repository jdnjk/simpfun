plugins {
    id 'com.android.application'
}
def isGithubActions = System.getenv('GITHUB_ACTIONS') == 'true'

def signingProps = new Properties()
def hasSignConfig = false
if (!isGithubActions) {
    def signFile = rootProject.file('sign.properties')
    if (signFile.exists()) {
        signFile.withInputStream { stream -> signingProps.load(stream) }
        hasSignConfig = true
    }
}

android {
    namespace 'cn.jdnjk.simpfun'
    compileSdk 36

    defaultConfig {
        buildConfigField "String", "VERSION_NAME", "\"1.1.1\""
        buildConfigField 'int', 'VERSION_CODE', "1110"
        applicationId "cn.jdnjk.simpfun"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 1110
        versionName "1.1.1"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
        
        def buglyIdEnv = System.getenv("BUGLY_ID")
        def buglyIdProp = project.hasProperty("bugly_id") ? project.getProperty("bugly_id") : null
        def buglyId = buglyIdEnv ?: buglyIdProp ?: null
        if (buglyId != null) {
            buildConfigField "String", "BUGLY_ID", "\"${buglyId}\""
        } else {
            buildConfigField "String", "BUGLY_ID", "null"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (hasSignConfig) {
            release {
                storeFile file(signingProps['storeFile'])
                storePassword signingProps['storePassword']
                keyAlias signingProps['keyAlias']
                keyPassword signingProps['keyPassword']
            }
            debug {
                storeFile file(signingProps['storeFile'])
                storePassword signingProps['storePassword']
                keyAlias signingProps['keyAlias']
                keyPassword signingProps['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (hasSignConfig) {
                signingConfig signingConfigs.release
                minifyEnabled true
                shrinkResources true
            }
        }
        debug {
            if (hasSignConfig) {
                signingConfig signingConfigs.debug
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    //noinspection Aligned16KB
    implementation 'com.tencent.bugly:crashreport:latest.release'

    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.github.Fox2Code.AndroidANSI:library:1.2.0'
    implementation 'com.github.bumptech.glide:glide:5.0.5'

    implementation 'io.github.rosemoe:editor:0.24.4'
    implementation 'io.github.rosemoe:language-textmate:0.24.4'
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.5")

    api 'com.alipay.sdk:alipaysdk-android:15.8.40'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.navigation:navigation-fragment:2.9.7'
    implementation 'androidx.navigation:navigation-ui:2.9.7'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}